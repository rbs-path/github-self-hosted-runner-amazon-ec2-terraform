# Auto Scaling lifecycle hook for termination
resource "aws_autoscaling_lifecycle_hook" "runner_termination" {
  name                    = "${var.name}-termination-hook"
  autoscaling_group_name  = aws_autoscaling_group.github_runner.name
  default_result          = "ABANDON"
  heartbeat_timeout       = 300 # 5 minutes
  lifecycle_transition    = "autoscaling:EC2_INSTANCE_TERMINATING"
  notification_target_arn = aws_sns_topic.runner_lifecycle.arn
  role_arn                = aws_iam_role.lifecycle_hook.arn
}


resource "aws_security_group" "lambda" {
  name        = "${var.name}-lambda-sg"
  description = "Security group for Lambda function"
  vpc_id      = data.terraform_remote_state.aws_account.outputs.hub_vpc_id

  tags = {
    Name              = "${var.name}-lambda-sg"
    cactus_exclusions = "public"
  }
}

resource "aws_security_group_rule" "lambda_egress" {
  type              = "egress"
  from_port         = 0
  to_port           = 0
  protocol          = "-1"
  cidr_blocks       = ["0.0.0.0/0"]
  description       = "Allow all outbound traffic for Lambda"
  security_group_id = aws_security_group.lambda.id
  #checkov:skip=CKV_AWS_382: Ensure no security groups allow egress from 0.0.0.0:0 to port -1
  #Reason: The Lambda requires access to GitHub to run the deregister code
  #Reason: The Lambda instances are sufficiently protected since they're in private subnet
}
# Lambda function for runner deregistration
resource "aws_lambda_function" "runner_deregistration" {
  filename                       = "runner_deregistration.zip"
  function_name                  = "${var.name}-deregistration"
  source_code_hash               = data.archive_file.lambda_zip.output_base64sha256
  role                           = aws_iam_role.lambda_deregistration.arn
  handler                        = "index.handler"
  runtime                        = "python3.12"
  timeout                        = 60
  reserved_concurrent_executions = 5
  kms_key_arn                    = aws_kms_key.encrypt_lambda.arn
  environment {
    variables = {
      SECRET_NAME         = data.aws_secretsmanager_secret.github_app.name
      REGION              = "eu-west-2"
      GITHUB_ORGANIZATION = var.github_organization
      LIFECYCLE_LOG_GROUP = aws_cloudwatch_log_group.github_runner_lifecycle.name
    }
  }
  vpc_config {
    subnet_ids         = data.terraform_remote_state.aws_account.outputs.hub_vpc_private_subnet_ids
    security_group_ids = [aws_security_group.lambda.id]
  }
  tracing_config {
    mode = "Active"
  }
  dead_letter_config {
    target_arn = aws_sqs_queue.dlq.arn
  }
  layers     = [aws_lambda_layer_version.lambda_layer_pyjwt.arn]
  depends_on = [data.archive_file.lambda_zip]
  #checkov:skip=CKV_AWS_272: Lambda function should be configured to validate code-signing
  #Reason: Code signing not required for internal automation Lambda function
}
# Lambda deployment package (code only, dependencies in layer)
data "archive_file" "lambda_zip" {
  type        = "zip"
  output_path = "runner_deregistration.zip"
  source {
    content  = file("${path.module}/lambda_package/lambda_deregistration.py")
    filename = "index.py"
  }
}

# IAM role for lifecycle hook
resource "aws_iam_role" "lifecycle_hook" {
  name = "${var.name}-lifecycle-hook-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          Service = "autoscaling.amazonaws.com"
        }
      }
    ]
  })
}

# IAM policy for lifecycle hook
resource "aws_iam_role_policy" "lifecycle_hook" {
  name = "${var.name}-lifecycle-hook-policy"
  role = aws_iam_role.lifecycle_hook.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "sns:Publish"
        ]
        Resource = aws_sns_topic.runner_lifecycle.arn
      },
      {
        Effect = "Allow"
        Action = [
          "kms:Encrypt*",
          "kms:Decrypt*",
          "kms:ReEncrypt*",
          "kms:GenerateDataKey*",
          "kms:Describe*"
        ]
        Resource = aws_kms_key.encrypt_sns.arn
      }
    ]
  })
}

# Lambda IAM role
resource "aws_iam_role" "lambda_deregistration" {
  name = "${var.name}-lambda-deregistration-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          Service = "lambda.amazonaws.com"
        }
      }
    ]
  })
}

# AWS managed policy for Lambda VPC execution
resource "aws_iam_role_policy_attachment" "lambda_vpc_execution" {
  role       = aws_iam_role.lambda_deregistration.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaVPCAccessExecutionRole"
}

# Lambda IAM policy
resource "aws_iam_role_policy" "lambda_deregistration" {
  name = "${var.name}-lambda-deregistration-policy"
  role = aws_iam_role.lambda_deregistration.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "logs:CreateLogGroup",
          "logs:CreateLogStream",
          "logs:PutLogEvents",
          "logs:DescribeLogStreams"
        ]
        Resource = "arn:aws:logs:eu-west-2:*:*"
      },
      {
        Effect = "Allow"
        Action = [
          "secretsmanager:GetSecretValue"
        ]
        Resource = [data.aws_secretsmanager_secret_version.github_app.arn]
      },
      {
        Effect = "Allow"
        Action = [
          "kms:Decrypt",
          "kms:Encrypt",
        ]
        Resource = [
          "arn:aws:kms:eu-west-2:830138816992:key/041268dc-8044-4479-a527-c836c8a81bc9",
          aws_kms_key.encrypt_lambda.arn,
        ]
      },
      {
        Effect = "Allow"
        Action = [
          "logs:CreateLogStream",
          "logs:PutLogEvents"
        ]
        Resource = [
          "${aws_cloudwatch_log_group.github_runner_lifecycle.arn}:*"
        ]
      },
      {
        Effect = "Allow"
        Action = [
          "autoscaling:CompleteLifecycleAction"
        ]
        Resource = [aws_autoscaling_group.github_runner.arn]
      },
      {
        Effect = "Allow"
        Action = [
          "sqs:SendMessage"
        ]
        Resource = [aws_sqs_queue.dlq.arn]
      }
    ]
  })
}

#https://registry.terraform.io/providers/hashicorp/aws/latest/docs/resources/sqs_queue
resource "aws_sqs_queue" "dlq" {
  name                              = "${var.name}-lambda-dlq"
  kms_master_key_id                 = aws_kms_key.encrypt_lambda.arn
  kms_data_key_reuse_period_seconds = 300
}
